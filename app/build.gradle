plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.moyerun.moyeorun_android"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def localProperties = new Properties()
        localProperties.load(rootProject.file('./local.properties').newDataInputStream())
        buildConfigField("String", "BASE_URL", localProperties['baseUrl'])
        buildConfigField("String", "WEB_CLIENT_ID", localProperties["webClientId"])
    }

    signingConfigs {
        debug {
            def keystoreProperties = new Properties()
            keystoreProperties.load(rootProject.file("./app/keystore_debug.properties").newDataInputStream())

            storeFile file(keystoreProperties["keystorePath"])
            storePassword keystoreProperties["storePassword"]
            keyPassword keystoreProperties["keyPassword"]
            keyAlias keystoreProperties["keyAlias"]
        }
        release {
            def keystoreProperties = new Properties()
            keystoreProperties.load(rootProject.file("./app/keystore.properties").newDataInputStream())

            storeFile file(keystoreProperties["keystorePath"])
            storePassword keystoreProperties["storePassword"]
            keyPassword keystoreProperties["keyPassword"]
            keyAlias keystoreProperties["keyAlias"]
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    def glide_version = '4.13.1'
    def datastore_preferences_version = '1.0.0'
    def retrofit_version = '2.9.0'
    def navigation_version = '2.4.2'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.activity:activity-ktx:1.4.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Lifecycle
    def lifecycle_version = '2.4.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:29.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Google Login
    implementation 'com.google.android.gms:play-services-auth:20.1.0'

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"


    // DataStore
    implementation "androidx.datastore:datastore-preferences:$datastore_preferences_version"
    implementation "androidx.datastore:datastore-core:$datastore_preferences_version"

    // Retrofit2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:4.10.0"

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
}